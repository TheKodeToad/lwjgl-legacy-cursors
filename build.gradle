import org.gradle.internal.jvm.Jvm
import org.gradle.internal.os.OperatingSystem

plugins {
	id 'java-library'
	id 'c'
}

repositories {
	mavenCentral()
}

String jvmHome = Jvm.current().javaHome

java {
	sourceCompatibility 8
	targetCompatibility 8
}

model {
	platforms {
		x64 {
			architecture 'x86_64'
		}
	}
	
	components {
		if (OperatingSystem.current() == OperatingSystem.LINUX) {
			lwjglLegacyCursorsX11(NativeLibrarySpec) {
				targetPlatform "x64"
				binaries.all {
					cCompiler.args '-I', "${jvmHome}/include"
					cCompiler.args '-I', "${jvmHome}/include/linux"
					cCompiler.args '-I', "/usr/include/X11"
					cCompiler.args '-D_FILE_OFFSET_BITS=64'
					linker.args '-lXcursor'
				}
			}
		}
		if (OperatingSystem.current() == OperatingSystem.WINDOWS) {
			lwjglLegacyCursorsWin32(NativeLibrarySpec) {
				targetPlatform "x64"
				binaries.all {
					cCompiler.args '-I', "${jvmHome}/include"
					cCompiler.args '-I', "${jvmHome}/include/win32"
					
					//String kit = "C:/Program Files (x86)/Windows Kits/10/Include/${windowsKitVersion}"
					//cCompiler.args '-I', "${kit}/um"
					//cCompiler.args '-I', "${kit}/shared"
				}
			}
		}
	}
}

dependencies {
	implementation 'org.lwjgl.lwjgl:lwjgl:2.9.1'
}
